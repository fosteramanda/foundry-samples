## Create workspace-based Application Insights for project tracing
resource "azurerm_log_analytics_workspace" "log_analytics" {
  name                = local.log_analytics_workspace_name
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
  sku                 = "PerGB2018"
  retention_in_days   = 30
}

resource "azurerm_application_insights" "app_insights" {
  name                         = local.application_insights_name
  location                     = azurerm_resource_group.rg.location
  resource_group_name          = azurerm_resource_group.rg.name
  workspace_id                 = azurerm_log_analytics_workspace.log_analytics.id
  application_type             = "web"
  internet_ingestion_enabled   = true
  internet_query_enabled       = true
  local_authentication_enabled = !var.use_project_managed_identity_for_trace_ingestion
}

locals {
  monitoring_metrics_publisher_role_definition_id = "/subscriptions/${data.azurerm_client_config.current.subscription_id}/providers/Microsoft.Authorization/roleDefinitions/3913510d-42f4-4e42-8a64-420c390055eb"
  application_insights_reader_role_definition_ids = toset([
    "/subscriptions/${data.azurerm_client_config.current.subscription_id}/providers/Microsoft.Authorization/roleDefinitions/73c42c96-874c-492b-b04d-ab87d138a893",
    "/subscriptions/${data.azurerm_client_config.current.subscription_id}/providers/Microsoft.Authorization/roleDefinitions/dbc9c667-e97f-4491-aee6-90b9cf960190"
  ])
}

resource "azurerm_role_assignment" "monitoring_metrics_publisher" {
  count                            = var.use_project_managed_identity_for_trace_ingestion ? 1 : 0
  scope                            = azurerm_application_insights.app_insights.id
  role_definition_id               = local.monitoring_metrics_publisher_role_definition_id
  principal_id                     = local.uai_principal_id
  skip_service_principal_aad_check = true
}

resource "azurerm_role_assignment" "application_insights_reader" {
  for_each                         = local.application_insights_reader_role_definition_ids
  scope                            = azurerm_application_insights.app_insights.id
  role_definition_id               = each.value
  principal_id                     = local.uai_principal_id
  skip_service_principal_aad_check = true
}

## Connect Application Insights to the Foundry project
resource "azapi_resource" "application_insights_connection" {
  type                      = "Microsoft.CognitiveServices/accounts/projects/connections@2025-09-01"
  name                      = local.application_insights_connection_name
  parent_id                 = azapi_resource.ai_project.id
  schema_validation_enabled = false

  body = {
    properties = merge({
      category      = "AppInsights"
      target        = azurerm_application_insights.app_insights.id
      authType      = var.use_project_managed_identity_for_trace_ingestion ? "ProjectManagedIdentity" : "ApiKey"
      isSharedToAll = var.use_project_managed_identity_for_trace_ingestion ? false : var.is_application_insights_connection_shared_to_all
      metadata = {
        ApiType                             = "Azure"
        ResourceId                          = azurerm_application_insights.app_insights.id
        ApplicationInsightsConnectionString = azurerm_application_insights.app_insights.connection_string
      }
      }, var.use_project_managed_identity_for_trace_ingestion ? {} : {
      credentials = {
        key = azurerm_application_insights.app_insights.connection_string
      }
    })
  }
}
